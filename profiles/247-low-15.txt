Controleo3 Profile

// Notes:
// 1. Comment lines can start with # or //
// 2. Comments can start after a token (e.g. Door close 1s // Close the oven door in 1 second)
// 3. Tokens are case-insensitive
// 4. End of line character can be CR or LF (or both, in any order)
// 5. Number delimiters can be any non-digit character (30/40/50 = 30,40,50 = 30 40 50)
// 6. When reading in profile files, Arduino's Serial Monitor will show debugging information


Name "247-LOWTEMP-15"
Title "Reflow"
// Show a graph, including the liquid temperature
Show graph to 175C for 600 seconds
Graph divider at 137C
// The deviation (in Celsius) above which to abort
Deviation to abort 20C
// The maximum temperature.  If exceeded, reflow will abort and the oven door opened
Maximum temperature 175C
// Set a maximum duty cycle to avoid damage to your oven (bottom/top/boost)
Maximum duty 100/75/60


// Open the oven door if it isn't already to let out heat
Open door over 2 seconds
Wait for 2 seconds
Stop timer
// Wait for the oven to cool to get consistent results
Display "Waiting for oven to cool to 80~C"
Wait until below 80C
Close door over 5 seconds

// Start the reflow
Display "Phase 1: Pre-Soak Manual Ramp"
// Turn on elements for a short while to heat them up
Element duty cycle 80/60/30
Wait until above 75C

Initialize timer to 15 seconds
Start plotting at 15 seconds
// Select the heating bias (bottom/top/boost)
Bias 100/80/60
// Continue ramping up the temperature using PID
Display "Phase 2: Pre-Soak Autoramp"
Ramp temperature to 110C in 80s

// Start the soak phase
Display "Phase 3: Soak"
// Select the heating bias (bottom/top/boost)
Bias 100/50/20
Ramp temperature to 125C in 100s

// Start the reflow phase
Display "Phase 4: Reflow Manual Ramp"
Element duty cycle 100/100/100
Wait for 10 seconds
// Select the heating bias (bottom/top/boost)
Bias 100/80/60
Display "Phase 5: Reflow Autoramp"
Ramp temperature to 165C in 60s

// Hold the temperature at peak for a few seconds
Display "Phase 6: Reflow Hold"
// Maintain peak temperature for a short while
Maintain 165C for 40 seconds
// Stop the timer so we know how long the reflow took
Stop timer
// Turn the elements off now (bottom/top/boost)
Element duty cycle 0/0/0

// Start Cooling
Display "Phase 7: Cooling to 100~C"
// Open the oven door
Open door over 10 seconds
// Wait for the door to open
Wait for 10 seconds
// Wait for the temperature to drop
Wait until below 100C

// Boards can be removed now
Display "Boards can be removed now"
// Play a tune that the reflow is done
Play tune
// Wait for the temperature to drop further
Wait until below 50C

// Reflow is done now
Display "Reflow complete!"
// Close the oven door
Close door over 3 seconds
